apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
    api 'com.mytechia:robobo-framework:1.3.4'
    api 'com.mytechia:robobo-remote-control:1.0.2'
    implementation group: 'com.alphacephei', name: 'vosk-android', version: '0.3.47'
}

tasks.register('genUUID') {
    def uuid_en = UUID.randomUUID().toString()
    def odir_en = file("$buildDir/generated/assets/model-en-us")
    def ofile_en = file("$odir_en/uuid")
    def uuid_es = UUID.randomUUID().toString()
    def odir_es = file("$buildDir/generated/assets/model-es-es")
    def ofile_es = file("$odir_es/uuid")
    doLast {
        mkdir odir_en
        mkdir odir_es
        ofile_en.text = uuid_en
        ofile_es.text = uuid_es
    }
}

preBuild.dependsOn(genUUID)


/*
New local publish module
TODO: Make it able to release on OSS Sonatype to MavenCentral. Not needed by now, we can just upload to the local repo.

Using maven-publish and standard methods to push to maven.
To do the same on other modules, just replace the Chris Bane's script with this
Use gradle publishing:publishLibraryPublicationToLocalNexusRepository to upload artifacts to the repo

If the version ends in -SNAPSHOT, the snapshot repo will be selected.
All of the release info is stored in gradle.properties

Nexus auth should be done in the user's home gradle.properties file (uncommited for security reasons)
 */

afterEvaluate {
    publishing {
        repositories {
            maven {
                name 'LocalNexus'
                allowInsecureProtocol = true
                credentials {
                    username NEXUS_USER
                    password NEXUS_PASSWORD
                }
                if(VERSION_NAME.endsWith('-SNAPSHOT')) {
                    url SNAPSHOT_REPOSITORY_URL
                } else {
                    url RELEASE_REPOSITORY_URL
                }
            }
        }

        publications {
            library(MavenPublication) {
                from components.release
                groupId GROUP
                artifactId POM_ARTIFACT_ID
                version VERSION_NAME
            }
        }
    }
}